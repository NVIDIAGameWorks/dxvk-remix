/*
* Copyright (c) 2023-2024, NVIDIA CORPORATION. All rights reserved.
*
* Permission is hereby granted, free of charge, to any person obtaining a
* copy of this software and associated documentation files (the "Software"),
* to deal in the Software without restriction, including without limitation
* the rights to use, copy, modify, merge, publish, distribute, sublicense,
* and/or sell copies of the Software, and to permit persons to whom the
* Software is furnished to do so, subject to the following conditions:
*
* The above copyright notice and this permission notice shall be included in
* all copies or substantial portions of the Software.
*
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
* IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
* FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL
* THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
* LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
* FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
* DEALINGS IN THE SOFTWARE.
*/
#pragma once

#include "rtx/concept/ray_portal/ray_portal.slangh"
#include "rtx/utility/packing.slangh"
#include "rtx/algorithm/resolve_expanded.slangh"
#include "rtx/external/NRC.slangh"

// RNG state is not carried via PathState from RayGen to Hit shader, 
// but instead it is reconstructed using following explicit constants.
// Hit shaders reuse the RNG state within its scope
// Direction sample takes 3 samples - 1 for a lobe selection and 2 for a 2D uv sample
static const uint kRNGOffsetSampleFirstBounceDirection = 0;
// Russian roulette on first bounce takes the sampleOffset 4 (carried over as live RNG state)
// RNG state is not carried between Raygen and closest hit shaders (CHS), so RNG state is recreated
// at CHS start with a following explicit offset
static const uint kRNGOffsetSampleClosestHitShader = 4;

// Approximate number of rng samples per bounce
static const uint kApproxRNGSamplesPerBounce = 16;

#define NEE_CACHE_STATE_DISABLED 0
#define NEE_CACHE_STATE_LAST_ENABLED 1
#define NEE_CACHE_STATE_ENABLED 2

// Path state to be passed to functions (or the CHS) handling a path vertex.
struct PathState : IBasePayloadState
{
  // Note: These ray properties are passed into the function with the state of the
  // ray being cast as an input, and then set to the new state of the sampled ray
  // (if applicable) as an output.
  vec3 _origin;
  float accumulatedHitDistance; // unused (1b - sign bit)

  // Note: Radiance, throughput and hit distances are passed in and then written back out
  // so they can be accumulated into in the path tracing loop.
  vec3 radiance;
  float _segmentHitDistance;

  f16vec3 throughput;
  float16_t firstBounceHitDistance; // Note: The total hit distance of the resolve for the first bounce.
  uint16_t neeCacheCellOffset;
  uint16_t mediumMaterialIndex;
  float16_t coneSpreadAngle; // unused (1b - sign bit)
  float16_t _coneRadius; // unused (1b - sign bit)
  
  f16vec3 _direction;
  float16_t solidAnglePdf; // unused (1b - sign bit)
  uint8_t _flags; // continueResolving(bool:1b) 
                  // continuePath(bool:1b) 
                  // insideMedium(bool:1b) 
                  // decalEncountered(bool:1b) 
                  // restirGiHasHitGeometry(bool:1b)
                  // restirGiHasHitOpaque(bool:1b)
                  // enableNeeCacheAfterFirstBounce(bool:1b) 
                  // restirGiHasFoundRoughSurface(bool:1b) 
  uint8_t _rayMask;
  uint8_t _data; // PortalSpace(uint:2b), 
                 // indirectLightPortalID(uint:2b), 
                 // bounceIteration(uint:4b)
  uint8_t _roughness;
    
#if ENABLE_NRC

  uint nrcData0;  // NrcQuery:
                  //  uint packedPrefixThroughput
                  // NrcUpdate:
                  //  f16vec3 throughputFullPath.xy
  uint nrcData1;  // NrcQuery:
                  //  [0 - NRC_MAX_REQUIRED_BITS_IN_PATH_STATE_QUERY_BUFFER_INDEX] - uint queryBufferIndex  
                  //  [30] - bool writeFinalPathInfo
                  //  [31] - bool isNrcUpdate | !isNrcQuery
                  // NrcUpdate:
                  //  [0-15] f16vec3 throughputFullPath.z;
                  //  [30] - bool writeFinalPathInfo
                  //  [31] - bool isNrcUpdate | !isNrcQuery

  NrcPackableFloat primarySpreadRadius; ///< Approximated as `d^2 / cos` at primary hit.
  NrcPackableFloat cumulSpreadRadius; ///< Square root of the cumulative area spread at the current path vertex.

  NrcPackableUint packedData; ///< The number of vertices processed, flags and termination reason
  NrcPackableFloat brdfPdf;

  // NRC resets path state's throughput on each bounce, but some parts of the PT
  // require accrued throughput along the full path regardless
  // Note: valid for isNrcUpdate == true
  property f16vec3 throughputFullPath
  {
    get 
    {
      return f16vec3(
        uint16BitsToHalf(nrcData0),
        uint16BitsToHalf(nrcData0 >> 16),
        uint16BitsToHalf(nrcData1));
    }

    set
    {
      nrcData0 = uint(float16BitsToUint16(newValue.x))
               | (uint(float16BitsToUint16(newValue.y)) << 16);
      nrcData1 = (nrcData1 & 0xFFFF0000) 
               | uint(float16BitsToUint16(newValue.z));
    }
  }

  // Note: valid for isNrcQuery == true
  property uint packedPrefixThroughput
  {
    get { return nrcData0; }
    set { nrcData0 = newValue; }
  }

  // Note: valid for isNrcQuery == true
  property uint queryBufferIndex
  {
#if NRC_MAX_REQUIRED_BITS_IN_PATH_STATE_QUERY_BUFFER_INDEX == 26    
    get { return nrcData1 & NRC_QUERY_BUFFER_INDEX_MASK; }
    set { nrcData1 = (nrcData1 & ~NRC_QUERY_BUFFER_INDEX_MASK) | (newValue & NRC_QUERY_BUFFER_INDEX_MASK); }
#else
    Validate  bit utilization in nrcData1 and update the bit target in the if check
#endif    
  }

  property bool writeFinalPathInfo
  {
    get { return packedFlagGet(nrcData1, 1 << 30); }
    set { nrcData1 = packedFlagAssign(nrcData1, 1 << 30, newValue); }
  }

  // Returns whether the pixel is integrating a path for NrcUpdate 
  property bool isNrcUpdate 
  {
    get { return packedFlagGet(nrcData1, 1 << 31); }
    set { nrcData1 = packedFlagAssign(nrcData1, 1 << 31, newValue); }
  }

  // Returns whether the pixel is integrating a path for NrcQuery
  property bool isNrcQuery 
  {
    get { return !isNrcUpdate; }
    set { isNrcUpdate = !newValue; }
  }

  property NrcPathState nrcPathState 
  {
    get 
    {
      NrcPathState _nrcPathState;
      
      if (isNrcQuery)
      {
        _nrcPathState.queryBufferIndex = queryBufferIndex;
        _nrcPathState.packedPrefixThroughput = packedPrefixThroughput;
      }
      else // isNrcUpdate
      {
        // Unused in NRC update
        _nrcPathState.queryBufferIndex = 0;
        _nrcPathState.packedPrefixThroughput = 0;
      }

      _nrcPathState.primarySpreadRadius = primarySpreadRadius; 
      _nrcPathState.cumulSpreadRadius = cumulSpreadRadius;
      _nrcPathState.packedData = packedData;
      _nrcPathState.brdfPdf = brdfPdf;

      return _nrcPathState;
    }

    set
    {
      if (isNrcQuery)
      {
        queryBufferIndex = newValue.queryBufferIndex;
        packedPrefixThroughput = newValue.packedPrefixThroughput;
      } 

      primarySpreadRadius = newValue.primarySpreadRadius;
      cumulSpreadRadius = newValue.cumulSpreadRadius;
      packedData = newValue.packedData;
      brdfPdf = newValue.brdfPdf;
    }
  }

  // Property to get nrc pixel coordinate
  // if isNrcUpdate == true => returns nrc update pixel coordinate
  //  Note: this is different from gbuffer pixel coordinate for NRC update pixels. 
  // if isNrcQuery == true => returns nrc query pixel coordinate
  property u16vec2 pixelCoordinate 
  {
    get
    {
      if (isNrcQuery)
      {
        const u16vec2 nrcCombinedCoordinate = getDispatchRaysIndex().xy;
        return u16vec2(nrcCombinedCoordinate.x, nrcCombinedCoordinate.y -  cb.nrcArgs.numRowsForUpdate);
      }
      else
      {
        return Nrc::calculateUpdatePixelCoordinateFromCombinedCoordinate(getDispatchRaysIndex().xy, cb);
      }
    }
  }

  property uint8_t pathMaxBounces
  {
    get
    {
      if (isNrcUpdate)
      {
        return cb.nrcArgs.updatePathMaxBounces;
      }
      else
      {
        return cb.pathMaxBounces;
      }
    }
  }

  property RussianRouletteMode russianRouletteMode
  {
    get
    {
      if (isNrcUpdate)
      {
        // Specular throughput biases NRC, so use throughput based mode unconditionally during update
        return RussianRouletteMode::ThroughputBased;
      }
      else
      {
        return cb.russianRouletteMode;
      }
    }
  }
#else

  // Property to get pixel coordinate
  property u16vec2 pixelCoordinate 
  {
    get 
    {
      return getDispatchRaysIndex().xy;
    }
  }

  property uint8_t pathMaxBounces
  {
    get
    {
      return cb.pathMaxBounces;
    }
  }

  property RussianRouletteMode russianRouletteMode
  {
    get
    {
      return cb.russianRouletteMode;
    }
  }
#endif

  // Passthrough accessors
  property float3 origin 
  {
    get { return _origin; }
    set { _origin = newValue; }
  }

  property float16_t coneRadius 
  {
    get { return _coneRadius; }
    set { _coneRadius = newValue; }
  }

  property float segmentHitDistance 
  {
    get { return _segmentHitDistance; }
    set { _segmentHitDistance = newValue; }
  }

  property uint8_t rayMask 
  {
    get { return _rayMask; }
    set { _rayMask = newValue; }
  }

  // Flags
  // Note: Needed output for Resolve function. Indicates that more resolve work needs to happen
  // and hits should not be counted until finished. Could be omitted from input memory payload
  // if extra flag space is needed, but kept there for now for simplicity.
  property bool continueResolving
  {
      get { return packedFlagGet(_flags, 1 << 0); }
      set { _flags = packedFlagAssign(_flags, 1 << 0, newValue); }
  }

  // Note: Used as an input and output to indicate when the path should finish. Set to true either
  // when the max bounce count is hit or when terminated due to a russian roulette style heuristic.
  property bool continuePath
  {
      get { return packedFlagGet(_flags, 1 << 1); }
      set { _flags = packedFlagAssign(_flags, 1 << 1, newValue); }
  }

  // Note: Used to keep track of if a ray is inside a translucent medium or not, used as both an
  // input and an output to keep a persistent state across calls.
  property bool insideMedium
  {
      get { return packedFlagGet(_flags, 1 << 2); }
      set { _flags = packedFlagAssign(_flags, 1 << 2, newValue); }
  }

  property bool decalEncountered
  {
      get { return packedFlagGet(_flags, 1 << 3); }
      set { _flags = packedFlagAssign(_flags, 1 << 3, newValue); }
  }

  property bool restirGiHasHitGeometry
  {
      get { return packedFlagGet(_flags, 1 << 4); }
      set { _flags = packedFlagAssign(_flags, 1 << 4, newValue); }
  }

  property bool restirGiHasHitOpaque
  {
      get { return packedFlagGet(_flags, 1 << 5); }
      set { _flags = packedFlagAssign(_flags, 1 << 5, newValue); }
  }

  property bool enableNeeCacheAfterFirstBounce
  {
      get { return packedFlagGet(_flags, 1 << 6); }
      set { _flags = packedFlagAssign(_flags, 1 << 6, newValue); }
  }

  property bool restirGiHasFoundRoughSurface
  {
      get { return packedFlagGet(_flags, 1 << 7); }
      set { _flags = packedFlagAssign(_flags, 1 << 7, newValue); }
  }

  property bool directionAltered
  {
      get { return false; }
      set { }
  }

  // Packed members

  property f16vec3 direction
  {
      get { return _direction; }
      set { _direction = newValue; }
  }

  // Track the portal ID that an indirect ray crosses
  property PortalSpace2BitsType portalSpace
  {
      get { return (PortalSpace2BitsType)(_data & 0b11); }
      set { _data = (_data & (~0b11)) | ((uint)newValue); }
  }

  property uint8_t indirectLightPortalID
  {
      get { return (uint8_t)((_data >> 2) & 0b11); }
      set { _data = (_data & (~(0b11 << 2))) | ((newValue & 0b11) << 2); }
  }

  property uint8_t bounceIteration
  {
      get { return (_data >> 4) & 0xf; }
      set { _data = (_data & (~(0xf << 4))) | ((newValue & 0xf) << 4); }
  }

  property f16vec4 accumulatedRotation
  {
      get { return getIdentityQuaternion(); }
      set { }
  }

  property float16_t gbufferPerceptualRoughness
  {
      get { return unorm8ToF16(_roughness); }
      set { _roughness = f16ToUnorm8(newValue); }
  }

  // Methods

  // Calculates an RNG offset at a start of a bounce set in the path state
  uint calculateRNGOffset(uint sampleOffset) 
  {
    return bounceIteration * kApproxRNGSamplesPerBounce + sampleOffset;
  }

  static uint calculateRNGOffsetOnFirstBounce(uint sampleOffset) 
  {
    return sampleOffset;
  }

  f16vec3 calculateFullThroughput() 
  {
#if ENABLE_NRC
    if (isNrcUpdate)
    {
      return throughputFullPath * throughput;
    }
    else 
    {
      return throughput; 
    }
#else
    return throughput;
#endif  
  }

  // Note: pathState.bounceIteration >= pathState.pathMaxBounces case is not handled and expected to be handled by the caller
  bool calculateUseRussianRoulette()
  {
    bool useRussianRoulette = 
      cb.enableRussianRoulette && 
      bounceIteration >= cb.pathMinBounces;

#if ENABLE_NRC
    useRussianRoulette = useRussianRoulette && NrcCanUseRussianRoulette(nrcPathState);

    if (isNrcUpdate)
    {
      useRussianRoulette = useRussianRoulette && cb.nrcArgs.updateAllowRussianRoulette;
    }
#endif    

    return useRussianRoulette;
  }

  // Note: Using separate accumulation for particles and whatever else as while they should be visible in reflections
  // through indirect paths, they do not need to be evaluated with particularly high quality. This is only done for the
  // first bounce as past that all particles and similar things will be skipped anyways.
  bool getSeparateUnorderedApproximationsActive()
  {
    RNG rng = createRNG(pixelCoordinate, cb.frameIdx, 101);
    return cb.enableSeparateUnorderedApproximations &&
           cb.enableUnorderedResolveInIndirectRays &&
           bounceIteration <= 1 && 
           (!cb.enableProbabilisticUnorderedResolveInIndirectRays || getNextSampleBlueNoise(rng) <= getUnorderedApproximationSelectionProbability());
  }

  // Describe the rate at which we should perform unordered resolve for indirect rays.
  static const float16_t kUnorderedResolveSelectionProbabilityMin = 1.h/16.h;
  static const float16_t kUnorderedResolveSelectionProbabilityMax = 1.h/2.h;

  float16_t getUnorderedApproximationSelectionProbability()
  {
    return cb.enableProbabilisticUnorderedResolveInIndirectRays
             // Note: The use of gbuffer roughness here is just a rough estimator for how important a ray is to the overall path contribution. 
             //       There are better estimators, however this one is basically free, and provides good quality.  Ideally we want high probability
             //       when the ray is from a smooth material, and low probability when it is very rough.
             ? lerp(kUnorderedResolveSelectionProbabilityMax, kUnorderedResolveSelectionProbabilityMin, gbufferPerceptualRoughness)
             : 1.h;
  }

#if defined(RAY_PIPELINE) && defined(RT_SHADER_EXECUTION_REORDERING)
  bool shouldReorder(out uint coherenceHints, out uint numCoherenceHints)
  {
    coherenceHints = 0;
    numCoherenceHints = 0;

    if (cb.enableSeparateUnorderedApproximations &&
        cb.enableUnorderedResolveInIndirectRays) 
    {
      addCoherenceHintAsLeastImportant1Bit(getSeparateUnorderedApproximationsActive(), coherenceHints, numCoherenceHints);
    }

#if ENABLE_NRC
    // Note: it is slightly more performant to have the NRC mode hint second
    addCoherenceHintAsLeastImportant1Bit(isNrcUpdate, coherenceHints, numCoherenceHints);
#endif
    return true;
  }

#else
  bool shouldReorder(out uint coherenceHints, out uint numCoherenceHints)
  {
    coherenceHints = 0;
    numCoherenceHints = 0;
    return false;
  }
#endif
}

struct DirectPathTextures
{
  RWTexture2D<uint16_t> SharedFlags;
  RWTexture2D<uint16_t> SharedMediumMaterialIndex;
  RWTexture2D<float4> PrimaryDirectDiffuseLobeRadianceHitDistance;
  RWTexture2D<float4> PrimaryDirectSpecularLobeRadianceHitDistance;
  RWTexture2D<float4> SecondaryCombinedDiffuseLobeRadianceHitDistance;
  RWTexture2D<float4> SecondaryCombinedSpecularLobeRadianceHitDistance;
  RWTexture2D<float4> IndirectRayOriginDirection;
  RWTexture2D<float4> IndirectThroughputConeRadius;
  RWTexture2D<float> IndirectFirstHitPerceptualRoughness;
  RWTexture2D<float> PrimaryRtxdiIlluminance;
};

struct IndirectPathTextures
{
  Texture2D<float4> PrimaryWorldPositionWorldTriangleNormal;
  RWTexture2D<float4> IndirectRadianceHitDistance;
};

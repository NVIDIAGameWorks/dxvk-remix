/*
* Copyright (c) 2022-2025, NVIDIA CORPORATION. All rights reserved.
*
* Permission is hereby granted, free of charge, to any person obtaining a
* copy of this software and associated documentation files (the "Software"),
* to deal in the Software without restriction, including without limitation
* the rights to use, copy, modify, merge, publish, distribute, sublicense,
* and/or sell copies of the Software, and to permit persons to whom the
* Software is furnished to do so, subject to the following conditions:
*
* The above copyright notice and this permission notice shall be included in
* all copies or substantial portions of the Software.
*
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
* IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
* FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL
* THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
* LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
* FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
* DEALINGS IN THE SOFTWARE.
*/
#pragma once

#include "rtx/utility/noise.slangh"
#include "rtx/utility/color.slangh"
#include "rtx/utility/packing.slangh"
#include "rtx/utility/debug_view_helpers.slangh"
#include "rtx/utility/gbuffer_helpers.slangh"
#include "rtx/concept/ray/ray.slangh"
#include "rtx/pass/instance_definitions.slangh"
#include "rtx/concept/surface/surface.slangh"
#include "rtx/concept/surface_material/surface_material.slangh"
#include "rtx/concept/light/light.slangh"
#include "rtx/algorithm/integrator_helpers.slangh"
#include "rtx/algorithm/lighting.slangh"
#include "rtx/algorithm/resolve.slangh"
#include "rtx/algorithm/visibility.slangh"
#include "rtx/algorithm/nee_cache.h"
#include "rtx/algorithm/path_state.slangh"

struct IndirectSampleData
{
  float16_t firstSampledLobePdf;    
  float16_t firstSampledSolidAnglePdf;
  bool firstSampledLobeIsSpecular;
  bool insideMedium; 

  [mutating] void unpack(const uint encodedData) 
  {
    firstSampledLobePdf = abs(unpackFloat2x16(encodedData)).x;
    firstSampledSolidAnglePdf = abs(unpackFloat2x16(encodedData)).y;

    firstSampledLobeIsSpecular = packedFlagGet(encodedData, 1 << 15);
    insideMedium = packedFlagGet(encodedData, 1 << 31);
  } 

  uint pack()
  {
    uint encodedData = packFloat2x16(abs(f16vec2(firstSampledLobePdf, firstSampledSolidAnglePdf)));
    
    // Note: Encode flags into the sign bits of each float. This must be done manually because -0 may not be properly
    // stored if the value is 0 as -0 and +0 tend to be treated the same. Assumes the pdfs are always 0 or greater
    // Lobe pdf and and solid angle pdfs are expected to be >= 0
    encodedData = packedFlagSet(encodedData, (uint(firstSampledLobeIsSpecular) << 15) | (uint(insideMedium) << 31));

    return encodedData;
  }

  [mutating] void load(u16vec2 texelIndex, RWTexture2D<uint> encodedIndirectSampleDataTexture) 
  {
    unpack(encodedIndirectSampleDataTexture[texelIndex]);
  }

  [mutating] void load(u16vec2 texelIndex, Texture2D<uint> encodedIndirectSampleDataTexture) 
  {
    unpack(encodedIndirectSampleDataTexture[texelIndex]);
  }

  void store(u16vec2 texelIndex, RWTexture2D<uint> encodedIndirectSampleDataTexture) 
  {
    encodedIndirectSampleDataTexture[texelIndex] = pack();
  }

  bool isValidSample()
  {
    return firstSampledSolidAnglePdf > 0.h;
  }
}

float calculateSpreadAngleFromSolidAnglePdf(float16_t incomingSpreadAngle, float16_t solidAnglePdf)
{
  // Note: If the spread angle is exactly 1.0, this is almost certainly the result of sampling a dirac on something such as perfectly
  // smooth opaque or translucent objects as we use a PDF of 1 in these cases (so that division by the PDF works out properly), and in
  // these cases the typical spread angle from PDF math breaks down (as it assumes uniform or slightly biased sampling over a sphere of
  // solid angles rather than just sampling a specific direction always like is done with the dirac case).
  // This may occasionally generate a bad spread angle for something that accidently has a PDF of exactly 1 randomly, but this should
  // happen so rarely that it is not an issue in practice. Additionally, this allows us to simply check this case here rather than
  // use more complex logic to indicate when a dirac was sampled from a material (which would likely be slower performance wise).
  if (solidAnglePdf == 1.0f)
  {
    return incomingSpreadAngle;
  }

  // This is a guesstimate at the cone angle that is optimal for sampling textures for surfaces found by BRDF rays.
  // The math here is derived from the assumption that the sampling PDF is uniform in a certain cone.
  // This assumption is of course incorrect for any reasonable importance sampling scheme, but it seems to work here.
  //
  // So, if we have a cone with apex angle 2*Theta, then it subtends the following solid angle:
  //   W = 4*pi * sin^2(Theta/2)
  //   https://en.wikipedia.org/wiki/Solid_angle#Solid_angles_for_common_objects 
  //
  // From the assumption that the PDF is uniform in this cone, we set:
  //   W = 1.0 / pdfW,
  // and solve the resulting equation for Theta, getting the below expression.
  //
  // The 'indirectRaySpreadAngleFactor' is a tweakable parameter compensating for the fact that we take multiple samples
  // in the cone. Normally it should be in the [0..1] range. Set it to 0 to disable the spread angle widening.
  float16_t newSpreadAngle = 2.0 * asin(sqrt(saturate(1.0 / (4.0 * pi * solidAnglePdf)))) * cb.indirectRaySpreadAngleFactor;
  
  return max(incomingSpreadAngle, newSpreadAngle);
}

Ray sampleDirection(
  inout RNG randomState, inout bool insideMedium, inout vec3 throughput,
  Ray ray, MinimalRayInteraction minimalRayInteraction, MinimalSurfaceInteraction minimalSurfaceInteraction,
  PolymorphicSurfaceMaterialInteraction polymorphicSurfaceMaterialInteraction,
  out uint8_t sampledLobe, out float16_t sampledLobePdf, out float16_t solidAnglePdf,
  bool isPrimaryIndirectSample)
{
  // Sample from the material

  SurfaceMaterialInteractionSample surfaceMaterialInteractionSample;
  SurfaceMaterialInteractionLobeSample surfaceMaterialInteractionLobeSample;
  bool penetrateSurface = false;

  polymorphicSurfaceMaterialInteractionCalcSample(
    polymorphicSurfaceMaterialInteraction, randomState, minimalRayInteraction,
    surfaceMaterialInteractionSample, surfaceMaterialInteractionLobeSample,
    insideMedium, penetrateSurface, isPrimaryIndirectSample);

  sampledLobe = surfaceMaterialInteractionLobeSample.lobe;
  sampledLobePdf = surfaceMaterialInteractionLobeSample.pdf;
  solidAnglePdf = min(surfaceMaterialInteractionSample.solidAnglePdf, float16Max);

  // Create a ray from the material sample

  Ray sampledRay = rayCreateDirection(
    minimalRayInteraction, minimalSurfaceInteraction, ray, surfaceMaterialInteractionSample.inputDirection, penetrateSurface);

  sampledRay.spreadAngle = calculateSpreadAngleFromSolidAnglePdf(sampledRay.spreadAngle, solidAnglePdf);

  // Combine sample throughput into the throughput

  // Note: The lobe sample PDF occasionally may be 0 to indicate no valid sample was taken, so this check must be here to avoid
  // dividing by 0. When the lobe sample PDF is 0, the throughput should also be 0 to indicate no valid sample was taken (much
  // like the Surface Material Interaction's sample throughput will be set to 0 to indicate no valid direction sample was taken).
  if (surfaceMaterialInteractionLobeSample.pdf != 0.0f) {
    throughput *= surfaceMaterialInteractionSample.throughput / surfaceMaterialInteractionLobeSample.pdf;
  } else {
    throughput = vec3(0.0f);
  }

  return sampledRay;
}

Ray sampleDirection(
  inout RNG randomState, inout bool insideMedium, inout vec3 throughput,
  Ray ray, MinimalRayInteraction minimalRayInteraction, MinimalSurfaceInteraction minimalSurfaceInteraction,
  PolymorphicSurfaceMaterialInteraction polymorphicSurfaceMaterialInteraction,
  out uint8_t sampledLobe, bool isPrimaryIndirectSample)
{
  float16_t dummySampledLobePdf, dummySolidAnglePdf;

  return sampleDirection(
    randomState, insideMedium, throughput,
    ray, minimalRayInteraction, minimalSurfaceInteraction,
    polymorphicSurfaceMaterialInteraction, sampledLobe, dummySampledLobePdf, dummySolidAnglePdf, isPrimaryIndirectSample);
}

bool evalMediumChange(
  bool oldInsideMedium, bool insideMedium,
  PolymorphicSurfaceMaterialInteraction polymorphicSurfaceMaterialInteraction,
  inout uint16_t mediumMaterialIndex)
{
  // Set the material medium index if a medium was entered or exited
  // Note: This does not work for nested translucency as most recent medium entered is stored, but this is a fine thing to not support as
  // it is not super common. Additionally this path should only be invoked if PSTR is desired, and only in the case of actual thick translucency
  // which changes the medium flag.

  const bool enteredMedium = !oldInsideMedium && insideMedium;
  const bool exitedMedium = oldInsideMedium && !insideMedium;

  if (enteredMedium)
  {
    // Note: For now we always know the only type of material which can trigger this path is the translucent material, so no need to check it.
    const TranslucentSurfaceMaterialInteraction translucentSurfaceMaterialInteraction =
      translucentSurfaceMaterialInteractionCreate(polymorphicSurfaceMaterialInteraction);

    mediumMaterialIndex = translucentSurfaceMaterialInteraction.sourceSurfaceMaterialIndex;

    return true;
  }
  else if (exitedMedium)
  {
    mediumMaterialIndex = BINDING_INDEX_INVALID;

    return true;
  }

  return false;
}

bool evalNEESecondary(
  LightSample lightSample,
  uint8_t sampledTransportPortalIndex, 
  PortalSpace2BitsType portalSpace,
  uint8_t objectMask,
  bool pomOpaqueSurfaceEncountered,
  bool isViewModelSurface,
  Ray viewRay,
  MinimalRayInteraction minimalRayInteraction, 
  MinimalSurfaceInteraction minimalSurfaceInteraction,
  OpaqueSurfaceMaterialInteraction opaqueSurfaceMaterialInteraction,
  inout vec3 diffuseLight,
  inout vec3 specularLight,
  uint16_t surfaceIndex, float2 texcoords)
{

  // Setup and trace the visibility ray

  uint8_t rayMask = OBJECT_MASK_OPAQUE | (objectMask & OBJECT_MASK_ALL_DYNAMIC);
  rayMask |= (cb.enableIndirectTranslucentShadows) ? OBJECT_MASK_TRANSLUCENT : 0;
  rayMask |= (cb.enableIndirectAlphaBlendShadows) ? OBJECT_MASK_ALPHA_BLEND : 0;

  const bool isThinOpaqueSubsurface = isThinOpaqueSubsurfaceMaterial(opaqueSurfaceMaterialInteraction);

  // The visibility test function applies an offset to the surface position to avoid self
  // intersection, so the actual length is slightly different from the estimatedLength here.
  float estimatedLength = length(lightSample.position - minimalSurfaceInteraction.position);

  // A floating point value is composed of sign + exponent + mantissa. The floating point
  // precision problem usually happens in the mantissa level. Therefore reducing the mantissa
  // can avoid the precision issue. The ratio 0.98 is used as a lower bound here.
  // The floatOffset here is based on experiment.
  const uint floatOffset = 4096;
  float ratio = max(0.98, asfloat(asuint(estimatedLength) - floatOffset) / estimatedLength);

  VisibilityResult visibility = traceVisibilityRay<visibilityModeEnablePom | visibilityModeEnableTranslucentMaterials>(minimalSurfaceInteraction,
    lightSample.position, rayMask,
    sampledTransportPortalIndex, portalSpace, pomOpaqueSurfaceEncountered,
    minimalRayInteraction.coneRadius, viewRay.spreadAngle, ratio, isViewModelSurface, false,
    surfaceIndex, texcoords,
    isThinOpaqueSubsurface,
    opaqueSurfaceMaterialInteraction.shadingNormal);

  if (visibility.hasOpaqueHit)
  {
    diffuseLight = specularLight = 0;
    return false;
  }  
  
  // Accumulate throughput and hit distance
  const float lightSampleThroughput = lightSample.solidAnglePdf > 0 ? 1.0 / lightSample.solidAnglePdf : 0.0;

  // Accumulate Radiance

  const vec3 lightThroughput = lightSampleThroughput * vec3(visibility.attenuation);
  const vec3 attenuatedRadiance = lightThroughput;

  diffuseLight *= attenuatedRadiance;
  specularLight *= attenuatedRadiance;

  return true;
}

void evaluateUnshadowedLight(
  LightSample lightSample,
  OpaqueSurfaceMaterialInteraction opaqueSurfaceMaterialInteraction,
  MinimalRayInteraction minimalRayInteraction,
  f16vec3 inputDirection,
  out vec3 diffuseLight,
  out vec3 specularLight)
{
  if (dot(inputDirection, opaqueSurfaceMaterialInteraction.shadingNormal) < 0.0 &&
      (!cb.sssArgs.enableThinOpaque || !isThinOpaqueSubsurfaceMaterial(opaqueSurfaceMaterialInteraction)))
  {
    diffuseLight = specularLight = 0;
    return;
  }

  const SurfaceMaterialInteractionSplitWeight splitWeight = opaqueSurfaceMaterialInteractionCalcProjectedWeight(
    opaqueSurfaceMaterialInteraction, minimalRayInteraction, inputDirection);

  diffuseLight = lightSample.radiance * (splitWeight.diffuseReflectionWeight + splitWeight.diffuseTransmissionWeight);
  specularLight = lightSample.radiance * splitWeight.specularReflectionWeight;
}

float calculateRussianRouletteContinueProbability(
  PathState pathState,
  bool isOpaqueMaterial,
  bool isSpecularLobe,
  vec3 continuationThroughput,
  float16_t isotropicRoughness,
  float16_t hitDistance)
{
  if (pathState.russianRouletteMode == RussianRouletteMode::ThroughputBased)
  {
    // Note: Combine the continuation throughput with the path throughput into a combined throughput to use as the basis for russian
    // roulette. This is done because the path throughput contains the throughput up to this vertex, and the continuation throughput
    // represents the throughput of the current direction sample to be used to trace the next ray from this vertex.
    // As such, the probability to consider when continuing the path should be based on the combination of these values as that is roughly the
    // probability any light will be received from that next vertex.
    // This usually doesn't need to be done, but since we don't multiply this throughput into the path state's throughput by this point, we
    // have to do it manually like this. If that ever changes in the future this extra multiplication should be removed to not double-accumulate
    // the continuation throughput.
    const vec3 combinedThroughput = pathState.calculateFullThroughput() * continuationThroughput;
    const float combinedThroughputLuminance = calcBt709Luminance(combinedThroughput);

    // Note: Implicitly clamped into the 0-1 range by the fact that the luminance should be 0-inf and the min operation with the
    // max continue probability which should be in the range 0-1.
    return min(combinedThroughputLuminance, cb.russianRouletteMaxContinueProbability);
  }
  else
  {
    // The new mode emphasis specular details by tracing more rays at low roughness surfaces with short hit distance.
    // For example, at metal wall corners.
    float specularWeight = 1.0;
    if (isOpaqueMaterial)
    {
      // Give hit points with lower roughness, or shorter hit distance a higher specular weight
      if (isSpecularLobe)
      {
        specularWeight = (1.0 - roughnessToPerceptualRoughness(isotropicRoughness)) * saturate(cb.russianRouletteDistanceFactor / hitDistance);
      }
      else
      {
        specularWeight = 0.0;
      }
    }
    // Specular probability is usually higher than diffuse probability, thus a hit point with higher specular weight will be
    // more likely to continue tracing rays
    return lerp(cb.russianRouletteDiffuseContinueProbability, cb.russianRouletteSpecularContinueProbability, specularWeight);
  }
}
